// STT with playback example.
// Recognize during a minute also handling playback requests (advanced).
// CMD_* user events are supposed to be generated with AMI or ARI

context incoming {
	stt_with_playback_advanced => {
		Answer();
		WaitEventInit(); // Initialize event queue
		GRPCSTTBackground(); // Initialize background recognition
		
		// In real world these events should come from AMI or ARI
		UserEvent(CMD_PLAY_CONTROL,eventbody:1@play\,\,long_nature_sound); // Start playing nature sound at channel 1 (as background)
		UserEvent(CMD_PLAY_CONTROL,eventbody:play\,\,greeting);            // Start playing "greeting.*" file at channel 0 (default channel)
		UserEvent(CMD_PLAY_CONTROL,eventbody:&sleep\,\,2);                 // Enqueue waiting for 2 seconds at channel 0
		UserEvent(CMD_PLAY_CONTROL,eventbody:&play\,\,what_shall_we_do);   // Enqueue playing "what_shall_we_do.*" file at channel 0

		Set(CALL_END_TIME=$[${GET_TIME_NSEC(MONOTONIC)} + 60]); // Set end time to a minute after now
		Set(SLEEP_TIME=$[${CALL_END_TIME} - ${GET_TIME_NSEC(MONOTONIC)}]); // Calculate maximum wait time
		while (${SLEEP_TIME} > 0) { // While not timed out yet
			WaitEvent(${SLEEP_TIME}); // Wait until next event or timeout
			if (${WAITEVENTSTATUS} == SUCCESS) {
				Set(CurrentUTCTime=${GET_TIME_NSEC(UTC)});
				Set(CurrentUTCTimeString=${STRFTIME(${CurrentUTCTime},GMT,%FT%T)}.${CurrentUTCTime:-9:});
				switch (${WAITEVENTNAME}) {
				case GRPCSTT_X_REQUEST_ID:
					Log(NOTICE,[${CurrentUTCTimeString}] x-request-id = ${WAITEVENTBODY});
					break;
				case GRPCSTT_ASCII:
					Log(NOTICE,[${CurrentUTCTimeString}] Phrase(ASCII encoded) = ${WAITEVENTBODY});
					break;
				case GRPCSTT_UTF8:
					Log(NOTICE,[${CurrentUTCTimeString}] Phrase(UTF-8 encoded) = ${WAITEVENTBODY});
					break;
				case GRPCSTT_SESSION_FINISHED:
					// Something went wrong: recognition session closed
					Set(ARRAY(STATUS,ERROR_CODE,ERROR_MESSAGE)=${WAITEVENTBODY});
					if (${STATUS} == SUCCESS) {
						// Can't actually happen
						Log(NOTICE,Session finished successfully);
					} else {
						Log(NOTICE,Session finished with error ${ERROR_CODE}: ${ERROR_MESSAGE});
					}
					Set(CALL_END_TIME=0); // Breaking out of while loop
					break;
				case CMD_PLAY_CONTROL:
					// Lets just pass command as is
					PlayBackground(${WAITEVENTBODY});
					break;
				default:
				}
			} else {
				Log(NOTICE,WaitEvent() WAITEVENTSTATUS == ${WAITEVENTSTATUS}; ${WAITEVENTBODY});
			}
			Set(SLEEP_TIME=$[${CALL_END_TIME} - ${GET_TIME_NSEC(MONOTONIC)}]); // Update maximum wait time
		}
		Hangup();
	}
};
