// Basic TTS example with single main loop - recommended approach.
// Recognize during a minute logging recognition events with level NOTICE.
//
// !!NOTE!!
// Execute 'ami-request.sh' script during call (see README up above)

context incoming {
	tts_event_driven => {
		Answer();
		WaitEventInit(); // Initialize event queue
		PlayBackgroundInitGRPCTTS(); // Initialize synthesis

		Set(CALL_END_TIME=$[${GET_TIME_NSEC(MONOTONIC)} + 120]); // Set end time to 2 minutes after now
		Set(SLEEP_TIME=$[${CALL_END_TIME} - ${GET_TIME_NSEC(MONOTONIC)}]); // Calculate maximum wait time
		while (${SLEEP_TIME} > 0) { // While not timed out yet
			WaitEvent(${SLEEP_TIME}); // Wait until next event or timeout
			if (${WAITEVENTSTATUS} == SUCCESS) {
				switch (${WAITEVENTNAME}) {
				case CMD_SAY:
					PlayBackground(say,,${WAITEVENTBODY});
					break;
				case CMD_SAY_NEXT:
					PlayBackground(&say,,${WAITEVENTBODY});
					break;
				default:
				}
			}
			Set(SLEEP_TIME=$[${CALL_END_TIME} - ${GET_TIME_NSEC(MONOTONIC)}]); // Update maximum wait time
		}

		Hangup();
	}
};
